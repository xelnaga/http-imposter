apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'artifactory'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'net.xelnaga'
version = "1.0.${new Date().format('yyMMdd.HHmm')}"

ext {
    artifactId = 'http-imposter'
}

buildscript {
    repositories {
        maven { url 'http://gradle.artifactoryonline.com/gradle/libs' }
        maven { url 'http://gradle.artifactoryonline.com/gradle/plugins' }
        maven { url 'http://dl.bintray.com/jfrog/jfrog-jars' }
    }

    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.2.0')
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

artifactory {
    contextUrl = System.getenv('ARTIFACTORY_CONTEXT_URL')

    publish {
        repository {
            repoKey = 'libs-releases-local'
            username = System.getenv('ARTIFACTORY_USER')
            password = System.getenv('ARTIFACTORY_PASSWORD')
            maven = true
        }
    }

    resolve {
        repository {
            repoKey = 'libs-releases'
            username = System.getenv('ARTIFACTORY_USER')
            password = System.getenv('ARTIFACTORY_PASSWORD')
            maven = true
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

dependencies {
    
    groovy 'org.codehaus.groovy:groovy-all:2.0.7'

    compile 'javax.servlet:servlet-api:2.5',
            "com.google.code.gson:gson:2.2.2",
            'commons-codec:commons-codec:1.6',
            'commons-lang:commons-lang:2.6'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0',
                'org.springframework:spring-core:3.1.4.RELEASE',
                'org.springframework:spring-test:3.1.4.RELEASE',
                'junit:junit:4.10',
                'cglib:cglib-nodep:2.2.2',
                'org.objenesis:objenesis:1.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'httpDeployer'
        repository(url: System.properties['artifactoryStableUrl']) {
            authentication(userName: System.properties['artifactoryUserName'], password: System.properties['artifactoryPassword'])
        }
        pom.artifactId = artifactId
    }
}
